allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'

	repositories {
		mavenCentral()
	}
}

ext{
	productionBuild = System.properties['productionBuild'] == 'true' ? true : false
	uploadRepoKey = System.properties['uploadRepoKey'] ?: 'bds-bdio-snapshot'
	buildName = 'magpie-libraries'
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'checkstyle'

	sourceCompatibility = 1.8

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:all'
	}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
	
	checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = '9.3'

        if(System.properties['allowFailingCheckstyle'] != null){
            configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle'), 'severity' : 'warning' ]
        }else {
            configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle'), 'severity' : 'error' ]
        }

        sourceSets = [
            project.sourceSets.main,
            project.sourceSets.test
        ]
    }

	jar {
		manifest {
			attributes('Implementation-Title': "${-> project.description}",
			           'Implementation-Version': "${-> project.version}")
		}
	}
	
	task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
    	classifier = 'javadoc'
    	from javadoc
	}
	
	assemble.dependsOn sourcesJar
	
	publishing{
		publications{
			maven(MavenPublication){
				from components.java
				artifact sourcesJar
			}
		}
		
		repositories {
		    maven {
		      name = "OSSRH"
		      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
		      credentials {
		        username = System.getenv("MAVEN_USERNAME")
		        password = System.getenv("MAVEN_PASSWORD")
		      }
		    }
		  }
	}
}
